/* ----------------------------------------------
		Legnagyobb közös osztó
-------------------------------------------------*/
#pragma leftmost
start S;
term a, b;

// X := Y; Y változatlan marad
void Let(nterm X, nterm Y)
{
	nterm Z;
	all X = eps;
	all Y = Z X;
	all Z = Y;
}

// X := X - Y; Y := Y - X;
// és ha negatív lenne akkor nulla lesz
void Minus(nterm X, nterm Y)
{
	all (X = X) && (Y = eps) && (X = eps);
}

// X := Max(X, Y); Y := Min(X, Y);
void Rendez(nterm X, nterm Y)
{
	nterm Z;
	all (X = X) && (Y = eps) && (X = Z);
	if (! X = X) all (Y = X);
	all Z = X Y;
}

// X := [X/Y];	Y := X mod Y
void Oszt(nterm X, nterm Y)
{
	nterm Q, Z;
	Y = eps;
	while (X = Q)
	{
		Let(Z, Y);
		Minus(X, Z);
	}
	if (Z = eps) {
		Q = eps;
		all (Z = eps) && (Y = eps);
	} else all Y = eps;
	all Q = X;
}

// X := LNKO(X, Y); Y := 0
void LNKO(nterm X, nterm Y)
{
	nterm Z;
	Rendez(X, Y);
	Let(Z, Y);
	while (Y = Y)
	{
		Oszt(X, Y);
		Let(X, Z);
		Let(Z, Y);
	}
	all Z = eps;
}

void main()
{
	nterm A, B;
	S = A A A A
	    B B;
	LNKO(A, B);
// terminális jelekre cseréljük, hogy legyen felismert nyelv:
	all A = a; 	
}